MODULE EmployeeExpenseDop;

REQUIRE EmployeeExpense, Salary;

NAMESPACE Employee;

inInterval 'Входит в интервал' (Month m, INTEGER y, Month mFrom, INTEGER yFrom, Month mTo, INTEGER yTo) = 
    IF y*100+number(m) >= yFrom*100+number(mFrom) AND y*100+number(m) <= yTo*100+number(mTo) THEN TRUE ;

importFromSalary 'Импортировать' (Month mFrom, INTEGER yFrom, Month mTo, INTEGER yTo) {
    NEWSESSION {
        DELETE Expense e WHERE e IS Expense AND inInterval(month(e), year(e), mFrom, yFrom, mTo, yTo);
        
        FOR sumPay(Salary s) AND inInterval(month(s), year(s), mFrom, yFrom, mTo, yTo) DO NEW e = Expense {
            employee(e) <- employee(s);
            year(e) <- year(s);
            month(e) <- month(s);
            type(e) <- GROUP MAX ExpenseType t IF name(t) == 'Зарплата';
            sum(e) <- sumPay(s);
        }
        APPLY ;
    }
}

FORM importFromSalaryParameters 'Импорт из журнала зарплат'
    OBJECTS mFrom = Month PANEL, yFrom = INTEGER PANEL
    PROPERTIES mFrom 'Месяц' = name(mFrom) SELECTOR, yFrom 'Год' = VALUE(yFrom)
    OBJECTS mTo = Month PANEL, yTo = INTEGER PANEL
    PROPERTIES mTo 'Месяц' = name(mTo) SELECTOR, yTo 'Год' = VALUE(yTo)
    
    EVENTS ON INIT {
        SEEK importFromSalaryParameters.mFrom = extractMonth(currentDate());
        SEEK importFromSalaryParameters.yFrom = extractYear(currentDate());
        SEEK importFromSalaryParameters.mTo   = extractMonth(currentDate());
        SEEK importFromSalaryParameters.yTo   = extractYear(currentDate());
    }
    EVENTS ON OK {
        importFromSalary(mFrom, yFrom, mTo, yTo);
    }
;

DESIGN importFromSalaryParameters {
    NEW report {
        NEW header1 FIRST {
            caption = 'Начало периода';
            horizontal = TRUE;
            MOVE PROPERTY (mFrom) { charWidth = 15; }
            MOVE PROPERTY (yFrom) { charWidth = 5; }
        }
        NEW header2 {
            caption = 'Конец периода';
            horizontal = TRUE;
            MOVE PROPERTY (mTo) { charWidth = 15; }
            MOVE PROPERTY (yTo) { charWidth = 5; }
        }
    }
    MOVE TOOLBARBOX;
}

importFromSalaryParameters 'Импорт из журнала зарплат' {
    SHOW importFromSalaryParameters FLOAT ;
}

EXTEND FORM expenses PROPERTIES importFromSalaryParameters() DRAW o TOOLBAR;
