MODULE TimeEntryTotalDashboardvDop;

REQUIRE TimeEntryTotalDashboard, ProjectExternal;

NAMESPACE Project;

hours 'Трудозатраты' (Project p, DATE df, DATE dt) = GROUP SUM hours(TimeEntry t) IF date(t) >= df AND date(t) <= dt BY project(t);
hours 'Трудозатраты' (Employee e, Issue i, DATE df, DATE dt) = GROUP SUM hours(TimeEntry t) IF date(t) >= df AND date(t) <= dt BY employee(t), issue(t);

FORM hoursProjects 'Трудозатраты по проектам'
    OBJECTS df = DATE PANEL, dt = DATE PANEL
    PROPERTIES dFrom 'Дата с' = VALUE (df), dTo 'Дата по' =  VALUE (dt)
    
    OBJECTS p = Project
    PROPERTIES(p) READONLY name, nameExternal
    PROPERTIES(p,df,dt) READONLY hours
    FILTERS hours(p,df,dt)
    ORDERS hours(p,df,dt) DESC
    
    OBJECTS e = Employee
    PROPERTIES(e) READONLY fullName
    PROPERTIES(p,e,df,dt) READONLY hours
    FILTERS hours(p,e,df,dt)
    ORDERS hours(p,e,df,dt) DESC
    
    OBJECTS i = Issue
    PROPERTIES(i) READONLY number, name, descriptionString, dateTimeCreated, nameAuthor, nameAssignedTo, nameTracker
    PROPERTIES(i) open TOOLBAR 
    PROPERTIES(e,i,df,dt) READONLY hours
    FILTERS hours(e,i,df,dt),
            project(i)==p
    ORDERS number(i)
    
    EVENTS ON INIT {
        SEEK hoursProjects.df = firstDayOfMonth(currentDate());
        SEEK hoursProjects.dt = currentDate();
    }
;

DESIGN hoursProjects {
    OBJECTS {
        NEW dates FIRST {
            horizontal = TRUE;
            caption = 'Даты ';
            MOVE PROPERTY (dFrom);
            MOVE PROPERTY (dTo);
        }
        NEW tab {
            fill = 1;
            horizontal = TRUE;
            MOVE BOX(p) {
                caption = 'Проекты';
                fill = 1;
            }
            NEW tab2 {
                MOVE BOX(e) {caption = 'Сотрудники';}
                MOVE BOX(i) {caption = 'Задачи';}
                fill = 4;
            }
        }
        
    }    
}

NAVIGATOR {
    project {
        NEW hoursProjects;
    }
}

fullNameEmployee 'Сотрудник' (Absence a) = fullName(employee(a));

EXTEND FORM timeEntryDashboard
    OBJECTS df = DATE PANEL, dt = DATE PANEL
    PROPERTIES df 'Дата с' = VALUE(df), dt 'Дата по' = VALUE(dt)
    
    OBJECTS asat = (as = Absence, dd = DATE)
    PROPERTIES READONLY vdd=VALUE(dd), extractDOWName(dd)
    PROPERTIES READONLY fullNameEmployee(as), hours(as, dd), nameReason(as), dateFrom(as), dateTo(as) 
    ORDERS vdd, fullNameEmployee(as)
    FILTERS hours(as, dd),
            dd >= df AND dd <= dt
;

DESIGN timeEntryDashboard {
    headerTab  {
        NEW period {
            caption = 'За период';
            NEW filterPeriod {
                horizontal = TRUE;
                MOVE PROPERTY (df);
                MOVE PROPERTY (dt);
            }
            MOVE BOX (asat);
        }
    }
}
