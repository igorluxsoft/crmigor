MODULE CopyTables;

REQUIRE Salary, TempClassReportsDop;

NAMESPACE CopyTables;

dateTimeCopySalary 'Дата копии таблиц <Зарплата>' = DATA DATETIME ();
dateTimeCopyMoneyAccounting 'Дата копии таблиц <Движение денег>' = DATA DATETIME ();

isSalary 'Зарплата (3 таблицы)' = DATA LOCAL NESTED BOOLEAN ();
isMoneyAccounting 'Движение денег (1 таблица)' = DATA LOCAL NESTED BOOLEAN ();
strHelp '' = DATA LOCAL NESTED TEXT ();

FORM inputParameters 'Копирование таблиц'
    PROPERTIES() isSalary, isMoneyAccounting, strHelp
;

DESIGN inputParameters {
    OBJECTS {
        horizontal = FALSE;
        MOVE PROPERTY (isSalary());
        MOVE PROPERTY (isMoneyAccounting());
        NEW cnt {
            fill = 1;
            MOVE PROPERTY (strHelp()) {background = RGB(204, 255, 255); charHeight = 3;}
        }
    }
}


///// Таблицы <Зарплата>
CLASS ReportingMonthCopy 'Отчетный месяц (копия)';
month 'Месяц' = DATA Month (ReportingMonthCopy) ;
year 'Год' = DATA INTEGER (ReportingMonthCopy) ;
closed 'Закрыт' = DATA BOOLEAN (ReportingMonthCopy);
curRate 'Курс обмена, USD' =  DATA NUMERIC[20,13] (ReportingMonthCopy);
sumBonus 'Премия' =  DATA NUMERIC[12,2] (ReportingMonthCopy);
note 'Примечание' = DATA STRING (ReportingMonthCopy);

nameMonth 'Месяц' (ReportingMonthCopy r) = staticCaption(month(r));


CLASS TransferCopy 'Выплата (копия)';
number 'Номер документа' = DATA STRING[15] (TransferCopy) CHARWIDTH 5;
date 'Дата документа' = DATA DATE (TransferCopy);
month 'Месяц' = DATA Month (TransferCopy) ;
year 'Год' = DATA INTEGER (TransferCopy) ;
operation 'Операция' = DATA MoveMoneyOpreration (TransferCopy) ;
employee 'Сотрудник' = DATA Employee (TransferCopy) ;
sum 'Сумма' = DATA NUMERIC[12,2] (TransferCopy);
note 'Примечание' = DATA STRING (TransferCopy);
registerAccounting 'Регистр учета' = DATA RegisterAccounting (TransferCopy) ;
typePayment 'Вид платежа' = DATA TypePayment (TransferCopy);
type 'Тип записи' = DATA STRING[1] (TransferCopy);

numberMonth 'Месяц' (TransferCopy t) = number(month(t));
nameMonth 'Месяц' (TransferCopy t) = staticCaption(month(t)) CHARWIDTH 5;
nameOperation 'Операция' (TransferCopy t) = name(operation(t)) CHARWIDTH 5;
codeOperation 'Код операции' (TransferCopy t) = code(operation(t)) CHARWIDTH 5;
nameEmployee 'Сотрудник' (TransferCopy t) = fullName(employee(t)) CHARWIDTH 5;
codeRegisterAccounting 'Регистр учета' (TransferCopy t) = code(registerAccounting(t));
nameTypePayment 'Вид платежа' (TransferCopy t) = name(typePayment(t));


CLASS SalaryCopy 'Зарплата сотрудников (копия)';
month 'Месяц' = DATA Month (SalaryCopy) ;
year 'Год' = DATA INTEGER (SalaryCopy)  ;
employee 'Сотрудник' = DATA Employee (SalaryCopy) ;
sumSalary 'Оклад' = DATA NUMERIC[12,2] (SalaryCopy);
sumBonus1 'Премия-1' = DATA NUMERIC[12,2] (SalaryCopy);
sumBonus2 'Премия-2' = DATA NUMERIC[12,2] (SalaryCopy);
sumBonus3 'Премия-3' = DATA NUMERIC[12,2] (SalaryCopy);
sumBonusQuarter 'Премия за квартал' = DATA NUMERIC[12,2] (SalaryCopy);
ktu 'КТУ' = DATA NUMERIC[7,2] (SalaryCopy);
sumFine1 'Штраф-1'   = DATA NUMERIC[12,2] (SalaryCopy);
sumFine2 'Штраф-2'   = DATA NUMERIC[12,2] (SalaryCopy);
sumFine3 'Штраф-3'   = DATA NUMERIC[12,2] (SalaryCopy);
note1 'Примечание-1' = DATA STRING (SalaryCopy);
note2 'Примечание-2' = DATA STRING (SalaryCopy);
sumPay 'Сумма к выплате' = DATA NUMERIC[12,2] (SalaryCopy);
sumPayUSD 'Сумма к выплате, USD' = DATA NUMERIC[12,2] (SalaryCopy);
payExtra 'Доплатить (в бухгалтерию)' = DATA NUMERIC[12,2] (SalaryCopy);
payExtraCash 'Доплатить наличными' = DATA NUMERIC[12,2] (SalaryCopy);
deltaPay 'Отклонение' = DATA NUMERIC[12,2] (SalaryCopy);
deltaReal 'Отклонение реальное' = DATA NUMERIC[12,2] (SalaryCopy);
isReturn 'Возврат' = DATA BOOLEAN (SalaryCopy);

nameMonth 'Месяц' (SalaryCopy s) = staticCaption(month(s))  CHARWIDTH 7;
nameEmployee 'Сотрудник' (SalaryCopy s) = fullName(employee(s)) CHARWIDTH 15;
sumBonus 'Премия' (SalaryCopy s) = sumBonus1(s) (+) sumBonus2(s) (+) sumBonus3(s) (+) sumBonusQuarter(s);
sumFine 'Штраф' (SalaryCopy s) = sumFine1(s) (+) sumFine2(s) (+) sumFine3(s);

///// Таблица <Движение денег>

CLASS MoneyMoveCopy 'Движение денег (копия)';
registerAccounting 'Регистр учета' = DATA RegisterAccounting (MoneyMoveCopy);
operation 'Операция' = DATA MoveMoneyOpreration (MoneyMoveCopy);
analytics 'Аналитика' = DATA Analytics (MoneyMoveCopy);
date 'Дата' = DATA DATE (MoneyMoveCopy);
summa 'Сумма' = DATA NUMERIC[14,2] (MoneyMoveCopy);
summaUSD 'Сумма, USD' = DATA NUMERIC[14,2] (MoneyMoveCopy);
balance 'Остаток после операции' = DATA NUMERIC[14,2] (MoneyMoveCopy);
customer 'Клиент' = DATA Customer (MoneyMoveCopy);
legalEntity 'Организация' = DATA LegalEntity (MoneyMoveCopy);
employee 'Сотрудник' = DATA Employee (MoneyMoveCopy) ;
note 'Примечание' = DATA STRING  (MoneyMoveCopy) CHARWIDTH 5;
note2 'Примечание-2' = DATA STRING  (MoneyMoveCopy) CHARWIDTH 5;
dateTimeEntry 'Время создания' = DATA DATETIME (MoneyMoveCopy);
userEntry 'Создан пользователем' = DATA Employee (MoneyMoveCopy);
dateTimeKorr 'Время корр-ки' = DATA DATETIME (MoneyMoveCopy);
userKorr 'Корр-ка пользователем' = DATA Employee (MoneyMoveCopy);
file = DATA FILE (MoneyMoveCopy);

nameRegisterAccounting 'Регистр учета' = name(registerAccounting(MoneyMoveCopy m)) CHARWIDTH 7;
codeRegisterAccounting 'Регистр учета' = code(registerAccounting(MoneyMoveCopy m)) CHARWIDTH 7;
shortNameCurrency 'Валюта' = shortNameCurrency(registerAccounting(MoneyMoveCopy m));
nameOperation 'Операция' = name(operation(MoneyMoveCopy m)) CHARWIDTH 7;
codeOperation 'Код операции' = code(operation(MoneyMoveCopy m)) CHARWIDTH 3;
nameGrupOperation 'Группа операции' = nameGrupMoveMoneyOperation(operation(MoneyMoveCopy m)) CHARWIDTH 7;
nameAnalytics 'Аналитика' = name(analytics(MoneyMoveCopy m)) CHARWIDTH 7;
codeAnalytics 'Код аналитики' = code(analytics(MoneyMoveCopy m)) CHARWIDTH 3;
nameCustomer 'Клиент' (MoneyMoveCopy m) = name(customer(m)) CHARWIDTH 5;
nameLegalEntity 'Организация'(MoneyMoveCopy m) = name(legalEntity(m)) CHARWIDTH 5;
nameEmployee 'Сотрудник' (MoneyMoveCopy m) = fullName(employee(m)) CHARWIDTH 5;
nameUserEntry 'Создан пользователем' (MoneyMoveCopy m) = name(userEntry(m)) CHARWIDTH 5;
nameUserKorr 'Корр-ка пользователем' (MoneyMoveCopy m) = name(userKorr(m)) CHARWIDTH 5;
/////

copyTables 'Копирование таблиц' () {
    strHelp() <- 'Дата/время последней копии таблиц:\n <Зарплата> - ' + 
                (IF dateTimeCopySalary() THEN toChar(dateTimeCopySalary(),'DD.MM.YYYY HH24:MI') ELSE 'отсутствует') + '\n<Движение денег> - ' + 
                (IF dateTimeCopyMoneyAccounting() THEN toChar(dateTimeCopyMoneyAccounting(),'DD.MM.YYYY HH24:MI') ELSE 'отсутствует');
    
    DIALOG inputParameters DO NEWSESSION {
        
        IF isSalary() THEN {
            
            DELETE ReportingMonthCopy a WHERE a IS ReportingMonthCopy; // чистим таблицу с копией  <Отчетный месяц (копия)>
            
            FOR ReportingMonth r IS ReportingMonth NEW rc = ReportingMonthCopy DO {
                month(rc) <- month(r);
                year(rc) <- year(r)  ;
                closed(rc) <- closed(r) ;
                curRate(rc) <- curRate(r) ;
                sumBonus(rc) <- sumBonus(r) ;
                note(rc) <- note(r) ;
            }
            
            DELETE TransferCopy t WHERE t IS TransferCopy; // чистим таблицу с копией  <Выплаты (копия)>
            
            FOR Transfer t IS Transfer NEW tc = TransferCopy DO {
                number(tc) <- number(t);
                date(tc) <- date(t);
                month(tc) <- month(t);
                year(tc) <- year(t);
                operation(tc) <- operation(t);
                employee(tc) <- employee(t);
                sum(tc) <- sum(t);
                note(tc) <- note(t);
                registerAccounting(tc) <- registerAccounting(t);
                typePayment(tc) <- typePayment(t);
                type(tc) <- type(t);
            }
            
            DELETE SalaryCopy s WHERE s IS SalaryCopy; // чистим таблицу с копией  <Зарплата сотрудников (копия)>
            
            FOR Salary s IS Salary NEW sc = SalaryCopy DO {
                month(sc) <- month(s);
                year(sc) <- year(s);
                employee(sc) <- employee(s);
                sumSalary(sc) <- sumSalary(s);
                sumBonus1(sc) <- sumBonus1(s);
                sumBonus2(sc) <- sumBonus2(s);
                sumBonus3(sc) <- sumBonus3(s);
                sumBonusQuarter(sc) <- sumBonusQuarter(s);
                ktu(sc) <- ktu(s);
                sumFine1(sc) <- sumFine1(s);
                sumFine2(sc) <- sumFine2(s);
                sumFine3(sc) <- sumFine3(s);
                note1(sc) <- note1(s);
                note2(sc) <- note2(s);
                sumPay(sc) <- sumPay(s);
                sumPayUSD(sc) <- sumPayUSD(s);
                payExtra(sc) <- payExtra(s);
                payExtraCash(sc) <- payExtraCash(s);
                deltaPay(sc) <- deltaPay(s);
                deltaReal(sc) <- deltaReal(s);
                isReturn(sc) <- isReturn(s);
            }
            
            dateTimeCopySalary() <- currentDateTime();
            APPLY ;
        }
        
        IF isMoneyAccounting() THEN {
            
            DELETE MoneyMoveCopy m WHERE m IS MoneyMoveCopy; // чистим таблицу с копией  <Движение денег (копия)>
            
            FOR MoneyMove m IS MoneyMove NEW mc = MoneyMoveCopy DO {
                
                registerAccounting(mc) <- registerAccounting(m);
                operation(mc) <- operation(m);
                analytics(mc) <- analytics(m);
                date(mc) <- date(m);
                summa(mc) <- summa(m);
                summaUSD(mc) <- summaUSD(m);
                balance(mc) <- balance(m);
                customer(mc) <- customer(m);
                legalEntity(mc) <- legalEntity(m);
                employee(mc) <- employee(m);
                note(mc) <- note(m);
                note2(mc) <- note2(m);
                dateTimeEntry(mc) <- dateTimeEntry(m);
                userEntry(mc) <- userEntry(m);
                dateTimeKorr(mc) <- dateTimeKorr(m);
                userKorr(mc) <- userKorr(m);
                file(mc) <- file(m);
            }
            
            dateTimeCopyMoneyAccounting() <- currentDateTime();
            APPLY;
        }
        
        MESSAGE 'Копирование завершено';
        
    } ELSE MESSAGE 'Копирование отменено';
}


salariesRecovery 'Восстановление таблиц "Начисление зарплаты"' () {
    ASK '               ВНИМАНИЕ ! \n\n '+ 'Вся информация в таблицах "Начисление зарплаты" будет УДАЛЕНА. \n\n ' +
        'После удаления в таблицы "Начисление зарплаты" будет перенесена информация из КОПИИ (текущие журналы).\n\n'+
        'Дата/время копии: ' + toChar(dateTimeCopySalary(),'DD.MM.YYYY HH24:MI')+
        '\n\nНажмите "Да" для восстановления таблиц "Начисление зарплаты" или "Нет" для отказа' yes = YESNO DO {
        
        IF yes THEN {

            ASK 'Подтвердите восстановление таблиц "Начисление зарплаты" нажатием кнопки "Да"' yes2 = YESNO DO {

                IF yes2 THEN {
                    
                    DELETE ReportingMonth a WHERE a IS ReportingMonth; // чистим таблицу <Отчетный месяц>
                    
                    FOR ReportingMonthCopy rc IS ReportingMonthCopy NEW r = ReportingMonth DO {
                        month(r) <- month(rc) ;
                        year(r) <- year(rc) ;
                        closed(r) <- closed(rc) ;
                        curRate(r) <- curRate(rc) ;
                        sumBonus(r) <- sumBonus(rc) ;
                        note(r) <- note(rc) ;
                    }
                    
                    DELETE Transfer t WHERE t IS Transfer; // чистим таблицу <Выплаты>
                    
                    FOR TransferCopy tc IS TransferCopy NEW t = Transfer DO {
                        number(t) <- number(tc);
                        date(t) <- date(tc);
                        month(t) <- month(tc);
                        year(t) <- year(tc);
                        operation(t) <- operation(tc);
                        employee(t) <- employee(tc);
                        sum(t) <- sum(tc);
                        note(t) <- note(tc);
                        registerAccounting(t) <- registerAccounting(tc);
                        typePayment(t) <- typePayment(tc);
                        type(t) <- type(tc);
                    }
        
                    DELETE Salary s WHERE s IS Salary; // чистим таблицу <Зарплата сотрудников>
        
                    FOR SalaryCopy sc IS SalaryCopy NEW s = Salary DO {
                        month(s) <- month(sc);
                        year(s) <- year(sc);
                        employee(s) <- employee(sc);
                        sumSalary(s) <- sumSalary(sc);
                        sumBonus1(s) <- sumBonus1(sc);
                        sumBonus2(s) <- sumBonus2(sc);
                        sumBonus3(s) <- sumBonus3(sc);
                        sumBonusQuarter(s) <- sumBonusQuarter(sc);
                        ktu(s) <- ktu(sc);
                        sumFine1(s) <- sumFine1(sc);
                        sumFine2(s) <- sumFine2(sc);
                        sumFine3(s) <- sumFine3(sc);
                        note1(s) <- note1(sc);
                        note2(s) <- note2(sc);
                        sumPay(s) <- sumPay(sc);
                        sumPayUSD(s) <- sumPayUSD(sc);
                        payExtra(s) <- payExtra(sc);
                        payExtraCash(s) <- payExtraCash(sc);
                        deltaPay(s) <- deltaPay(sc);
                        deltaReal(s) <- deltaReal(sc);
                        isReturn(s) <- isReturn(sc);
                    }
                    APPLY ;
                    MESSAGE 'Восстановление завершено';
                }
                ELSE MESSAGE 'Восстановление отменено';
            }
            ELSE MESSAGE 'Восстановление отменено';
        }
        ELSE MESSAGE 'Восстановление отменено';
    }
    ELSE MESSAGE 'Восстановление отменено';
}


moneyAccountingRecovery 'Восстановление таблицы "Движение денег"' () {
    ASK '               ВНИМАНИЕ ! \n\n ' + 'Вся информация в таблице "Движение денег" будет УДАЛЕНА. \n\n ' +
        'После удаления в таблицу "Движение денег" будет перенесена информация из КОПИИ (текущий журнал).\n\n' +
        'Дата/время копии: ' + toChar(dateTimeCopyMoneyAccounting(), 'DD.MM.YYYY HH24:MI') +
        '\n\nНажмите "Да" для восстановления таблицы "Движение денег" или "Нет" для отказа' yes = YESNO DO {
        
        IF yes THEN {
            
            ASK 'Подтвердите восстановление таблицы "Движение денег" нажатием кнопки "Да"' yes2 = YESNO DO {

                IF yes2 THEN {
                    
                    DELETE MoneyMove m WHERE m IS MoneyMove; // чистим таблицу с копией  <Движение денег>
                    
                    FOR MoneyMoveCopy mc IS MoneyMoveCopy NEW m = MoneyMove DO {
                        
                        registerAccounting(m) <- registerAccounting(mc);
                        operation(m) <- operation(mc);
                        analytics(m) <- analytics(mc);
                        date(m) <- date(mc);
                        summa(m) <- summa(mc);
                        summaUSD(m) <- summaUSD(mc);
                        balance(m) <- balance(mc);
                        customer(m) <- customer(mc);
                        legalEntity(m) <- legalEntity(mc);
                        employee(m) <- employee(mc);
                        note(m) <- note(mc);
                        note2(m) <- note2(mc);
                        dateTimeEntry(m) <- dateTimeEntry(mc);
                        userEntry(m) <- userEntry(mc);
                        dateTimeKorr(m) <- dateTimeKorr(mc);
                        userKorr(m) <- userKorr(mc);
                        file(m) <- file(mc);
                    }
                    APPLY;
                    MESSAGE 'Восстановление завершено';
                }
                ELSE MESSAGE 'Восстановление отменено';
            }
            ELSE MESSAGE 'Восстановление отменено';
        }
        ELSE MESSAGE 'Восстановление отменено';
    }
    ELSE MESSAGE 'Восстановление отменено';
}


FORM salariesCopy 'Начисление зарплаты (КОПИЯ)'
    PROPERTIES READONLY 'Дата/время копии: ' =dateTimeCopySalary() PANEL
    PROPERTIES salariesRecovery() TOOLBAR 
    
    OBJECTS r = ReportingMonthCopy
    PROPERTIES(r) READONLY nameMonth, year, curRate, sumBonus, closed, note
    
    OBJECTS s = SalaryCopy
    PROPERTIES(s) READONLY  nameEmployee, sumSalary, sumBonus, sumFine, sumPay, sumPayUSD, payExtra, payExtraCash, deltaPay
    PROPERTIES(s) READONLY  deltaReal, isReturn, note1, note2, ktu
    PROPERTIES(s) READONLY  nameMonth, year
    ORDERS nameEmployee(s)
    FILTERS month(s)==month(r) AND year(s)==year(r)
    
    OBJECTS t = TransferCopy LAST
    PROPERTIES(t) READONLY numberMonth, nameMonth, year, number, date, nameEmployee, codeOperation, nameOperation, sum, note, nameTypePayment, codeRegisterAccounting
    ORDERS year(t), numberMonth(t), date(t), nameEmployee(t)
    
    FILTERGROUP transfer
        FILTER 'По сотруднику за месяц' employee(t)==employee(s) AND year(t)==year(s) AND month(t)==month(s) DEFAULT
        FILTER 'По сотруднику' employee(t)==employee(s)
        FILTER 'За месяц' year(t)==year(r) AND month(t)==month(r)
;

DESIGN salariesCopy {
    MOVE BOX(r) {
        fill=1;
    }
    MOVE BOX(s) {fill=10;}
    MOVE BOX(t) {fill=1;}
    MOVE TOOLBARBOX;
}


openFile 'Открыть' (MoneyMoveCopy m) { open(file(m)); }

FORM viewMoneyMoveCopy 'Движение денег (КОПИЯ)'
    PROPERTIES READONLY 'Дата/время копии: ' =dateTimeCopyMoneyAccounting() PANEL
    PROPERTIES moneyAccountingRecovery() TOOLBAR 
    
    OBJECTS m = MoneyMoveCopy LAST
    PROPERTIES(m) READONLY date, codeOperation, nameOperation, nameGrupOperation, nameAnalytics, codeAnalytics, codeRegisterAccounting, nameRegisterAccounting, 
        summa, balance, shortNameCurrency, summaUSD,
        nameCustomer, nameLegalEntity, nameEmployee, note, note2, dateTimeEntry, nameUserEntry, dateTimeKorr, nameUserKorr
    PROPERTIES(m) openFile READONLYIF NOT file(m)
    ORDERS date(m), codeOperation(m)
    
    OBJECTS mm = MoneyMoveCopy PANEL
    PROPERTIES(mm) READONLY note, note2, dateTimeEntry, nameUserEntry, dateTimeKorr, nameUserKorr
    FILTERS mm=m
;

DESIGN viewMoneyMoveCopy {
    OBJECTS {
        NEW headerBox {
            horizontal = TRUE;
            alignment = STRETCH;
            NEW cntrz {
                caption = 'Текущие значения';
                fill = 1;
                alignment = STRETCH;
                MOVE PROPERTY(note(mm));
                MOVE PROPERTY(note2(mm));
            }
            NEW cntrz2 {
                caption = 'Создание';
                MOVE PROPERTY(dateTimeEntry(mm)) {caption = 'Дата/время';}
                MOVE PROPERTY(nameUserEntry(mm)) {caption = 'Пользователь';}
            }
            NEW cntrz3 {
                caption = 'Корректировка';
                MOVE PROPERTY(dateTimeKorr(mm)) {caption = 'Дата/время';}
                MOVE PROPERTY(nameUserKorr(mm)) {caption = 'Пользователь';}
            }
            NEW ctrz4 {
                horizontal = TRUE;
                alignment = STRETCH;
                NEW loadFile {
                    caption = 'Файл';
                    horizontal = TRUE;
                    MOVE PROPERTY (openFile(m));
                }
            }
        }
        MOVE BOX(m) {fill=1;}
     }
}

NAVIGATOR {
    finance {
        NEW FOLDER copy 'Копирование' {
            NEW ACTION copyTables;
            NEW salariesCopy;
            NEW viewMoneyMoveCopy;
        }    
    }
}

///// Удаление записи в журнале "Регистры учета"
countRecordsInMoneyMove 'Количество записей по регистру учета' = GROUP SUM 1 BY registerAccounting(MoneyMove m);
countRecordsInMoneyMoveCopy 'Количество записей по регистру учета' = GROUP SUM 1 BY registerAccounting(MoneyMoveCopy mc);
countRecordsInTransfer 'Количество записей по регистру учета' = GROUP SUM 1 BY registerAccounting(Transfer t);
countRecordsInTransferCopy 'Количество записей по регистру учета' = GROUP SUM 1 BY registerAccounting(TransferCopy t);

deleteInRegisterAccounting 'Удалить' (RegisterAccounting r)  {
    // проверка наличия удаляемого регистра учета в журналах
    IF countRecordsInMoneyMove(r) THEN {
        MESSAGE 'Удаление регистра учета невозможно !\n\n Данный регистр присутствует в журнале "Движение денег"';
        RETURN ;
    }
    IF countRecordsInMoneyMoveCopy(r) THEN {
        MESSAGE 'Удаление регистра учета невозможно !\n\n Данный регистр присутствует в журнале "Движение денег (КОПИЯ)"';
        RETURN ;
    }
    IF countRecordsInTransfer(r) THEN {
        MESSAGE 'Удаление регистра учета невозможно !\n\n Данный регистр присутствует в журнале "Выплаты"';
        RETURN ;
    }
    IF countRecordsInTransferCopy(r) THEN {
        MESSAGE 'Удаление регистра учета невозможно !\n\n Данный регистр присутствует в журнале "Выплаты (КОПИЯ)"';
        RETURN ;
    }
    //
    
    NEWSESSION {
        ASK 'Вы действительно хотите удалить текущую запись  ?' DO {
            NEWSESSION NESTED LOCAL {
                DELETE r;
                APPLY ;
            }
        }
    }
} IMAGE 'delete.png';

EXTEND FORM viewRegisterAccounting
    PROPERTIES(r) deleteInRegisterAccounting TOOLBAR LAST
;

///// Отчет по сравнению сумм в журналах "Движение денег" и "Движение денег(КОПИЯ)" за интервал дат
//sumMm 'Сумма в журнале "Движение денег"' = DATA LOCAL NESTED NUMERIC[14,2] (DATE, RegisterAccounting, MoveMoneyOpreration, Analytics, Customer);
//sumCp 'Сумма в журнале "Движение денег(КОПИЯ)"' = DATA LOCAL NESTED NUMERIC[14,2] (DATE, RegisterAccounting, MoveMoneyOpreration, Analytics, Customer);
//countDiffSum 'Количество расхождений' = DATA LOCAL NESTED INTEGER ();
//
//FORM reportForComparisonSumMoneyMove 'Сверка сумм в журналах "Движение денег" и "Движение денег(КОПИЯ)"'
//    OBJECTS ob = (d = DATE, r = RegisterAccounting, o = MoveMoneyOpreration, a = Analytics, c = Customer)
//    PROPERTIES d = VALUE(d)
//    PROPERTIES code(r), code(o), code(a), name(a), name(c)
//    PROPERTIES sumMm(d,r,o,a,c), sumCp(d,r,o,a,c)
//    
//    ORDERS d, code(r), code(o), code(a), name(c)
//    FILTERS sumMm(d,r,o,a,c) (-) sumCp(d,r,o,a,c)
//;
//
//fillReportForComparisonSumMoneyMove 'Сформировать' (DATE df, DATE dt) {
//    sumMm(DATE d, RegisterAccounting r, MoveMoneyOpreration o, Analytics a, Customer c) <- NULL ;
//    sumCp(DATE d, RegisterAccounting r, MoveMoneyOpreration o, Analytics a, Customer c) <- NULL ;
//    
//    sumMm(DATE d, RegisterAccounting r, MoveMoneyOpreration o, Analytics a, Customer c) <- GROUP SUM summa(MoneyMove m) IF date(m)>=df AND date(m)<=dt AND date(m)=d AND 
//        registerAccounting(m)=r AND operation(m)=o AND analytics(m)=a AND customer(m)=c;
//    sumCp(DATE d, RegisterAccounting r, MoveMoneyOpreration o, Analytics a, Customer c) <- GROUP SUM summa(MoneyMoveCopy m) IF date(m)>=df AND date(m)<=dt AND date(m)=d AND
//        registerAccounting(m)=r AND operation(m)=o AND analytics(m)=a AND customer(m)=c;
//    
//    countDiffSum() <- GROUP SUM 1 IF sumMm(DATE d, RegisterAccounting r, MoveMoneyOpreration o, Analytics a, Customer c) (-) sumCp(d,r,o,a,c) ;
//    
//    IF countDiffSum() AND countDiffSum() > 0 THEN
//        PRINT reportForComparisonSumMoneyMove ;
//    ELSE MESSAGE 'Расхождений при сверке сумм не обнаружено';
//}

noReport 'Не нужно включать в отчет' = DATA LOCAL BOOLEAN (MoneyMove);
noReport 'Не нужно включать в отчет' = DATA LOCAL BOOLEAN (MoneyMoveCopy);

FORM reportForComparisonSumMoneyMove 'Сверка сумм в журналах "Движение денег" и "Движение денег(КОПИЯ)"'
    OBJECTS dd = (df = DATE, dt = DATE ) PANEL 
    PROPERTIES df = VALUE(df), dt = VALUE(dt)
    
    OBJECTS t = TempClassReportsDop
    PROPERTIES(t) pd1, ps1, ps2, ps3, ps4, pn1, pn2
    ORDERS pd1(t), ps1(t), ps2(t), ps3(t), ps4(t)
;

fillReportForComparisonSumMoneyMove 'Сформировать' (DATE df, DATE dt) {
    NEWSESSION {
        newTempClassReportsDop();
        noReport(MoneyMove m) <- NULL ;
        noReport(MoneyMoveCopy mcp) <- NULL ;
        LOCAL lFound = INTEGER ();

        FOR date(MoneyMove m) >= df AND date(m) <= dt DO {
            FOR date(MoneyMoveCopy mcp) = date(m) AND registerAccounting(m) = registerAccounting(mcp) AND operation(m) = operation(mcp) AND
                (analytics(m) = analytics(mcp) OR (NOT analytics(m) AND NOT analytics(mcp))) AND
                (customer(m) = customer(mcp) OR (NOT customer(m) AND NOT customer(mcp))) AND
                summa(m) = summa(mcp) AND NOT noReport(mcp) DO {

                noReport(m) <- TRUE ;
                noReport(mcp) <- TRUE ;
                BREAK ;
            }

        }

        FOR date(MoneyMove m) >= df AND date(m) <= dt AND NOT noReport(m) DO {
            NEW t = TempClassReportsDop {
                pd1(t) <- date(m);
                ps1(t) <- codeRegisterAccounting(m);
                ps2(t) <- OVERRIDE codeOperation(m), ' ';
                ps3(t) <- IF codeAnalytics(m) THEN CONCAT ' ', codeAnalytics(m), nameAnalytics(m) ELSE ' ';
                ps4(t) <- OVERRIDE nameCustomer(m), ' ';
                pn1(t) <- summa(m);
            }
        }
        
        FOR date(MoneyMoveCopy mcp) >= df AND date(mcp) <= dt AND NOT noReport(mcp) DO {
            
            lFound() <- 1 ;
            
            FOR pd1(TempClassReportsDop t)=date(mcp) AND ps1(t)=codeRegisterAccounting(mcp) AND ps2(t)=(OVERRIDE codeOperation(mcp),' ') AND
                ps3(t)=(IF codeAnalytics(mcp) THEN CONCAT ' ', codeAnalytics(mcp), nameAnalytics(mcp) ELSE ' ') AND 
                ps4(t)=(OVERRIDE nameCustomer(mcp), ' ') AND 
                NOT pn2(t) DO {
                
                pn2(t) <- summa(mcp);
                lFound() <- 0 ;
                BREAK ;
            }
            
            IF lFound() = 1 THEN {
                NEW t = TempClassReportsDop {
                    pd1(t) <- date(mcp);
                    ps1(t) <- codeRegisterAccounting(mcp);
                    ps2(t) <- OVERRIDE codeOperation(mcp), ' ';
                    ps3(t) <- IF codeAnalytics(mcp) THEN CONCAT ' ', codeAnalytics(mcp), nameAnalytics(mcp) ELSE ' ';
                    ps4(t) <- OVERRIDE nameCustomer(mcp), ' ';
                    pn2(t) <- summa(mcp);
                }
            }
        }
        
        IF countTempClassReportsDop() > 0 THEN
            PRINT reportForComparisonSumMoneyMove OBJECTS df = df, dt = dt XLSX ;
        ELSE MESSAGE 'Расхождений при сверке сумм не обнаружено';
    }
}    

FORM reportForComparisonSumMoneyMoveParameters 'Сверка сумм в журналах "Движение денег" и "Движение денег (Копия)" '
    OBJECTS df = DATE PANEL, dt = DATE PANEL
    PROPERTIES df 'Дата с' = VALUE(df), dt 'Дата по' = VALUE(dt)
    
    PROPERTIES fillReportForComparisonSumMoneyMove(df, dt)
    
    EVENTS ON INIT {
        SEEK reportForComparisonSumMoneyMoveParameters.df = firstDayOfMonth(currentDate());
        SEEK reportForComparisonSumMoneyMoveParameters.dt = currentDate();
    }
;

DESIGN reportForComparisonSumMoneyMoveParameters {
    NEW report {
        fill = 1;
        NEW params {
            MOVE PROPERTY(df);
            MOVE PROPERTY(dt);
        }
        MOVE PROPERTY(fillReportForComparisonSumMoneyMove(df, dt));
    }
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    finance {
        reports {
            NEW reportForComparisonSumMoneyMoveParameters;
        }
    }
}

